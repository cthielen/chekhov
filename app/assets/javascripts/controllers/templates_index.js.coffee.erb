Chekhov.controller "TemplatesIndexCtrl", @TemplatesIndexCtrl = ($scope, $modal, $location, Templates, Checklists, User, ChecklistStarter, TemplateCategories, State, $rootScope) ->
  $scope.loaded = false
  $scope.error = null
  $scope.display = State

  # Gets templates from the appropriate categories and sets the info text to
  # tell user which categories are selected
  $scope.getTemplates = ->
    selectedCategories = _.filter($scope.categories, (category) -> category.selected)
    $scope.display.categories = _.pluck(selectedCategories, "id")
    $scope.categoriesSelected = _.pluck(selectedCategories, "name").join(", ") || false

    Templates.query { 'categories[]': $scope.display.categories },
      (data) ->
        # Success
        $scope.templates = data
        $scope.loaded = true
        $rootScope.template_active = $scope.templates.length

    , (data) ->
        # Error
        $scope.error = "Error retrieving information from server"

  # Clears all selected categories
  $scope.resetFilters = () ->
    $scope.categories = _.map($scope.categories, (category) ->
      delete category.selected
      category
    )
    $scope.getTemplates()

  $scope.allTemplates = $scope.templates

  $scope.canManage = User.is_admin and $location.path() is "/templates/manage"
 
  $scope.activeTab = 1
  $scope.user = User

  console.debug 'TemplatesIndexCtrl', 'Initializing...'

  #$('ul.nav li').removeClass 'active'
  #$('ul.nav li#checklists_all').addClass 'active'
  
  # Starts a new checklist or edits the template, depending on which page
  # is being displayed. 
  $scope.actOn = (template) ->
    if $scope.canManage
        $scope.editChecklist template
    else ChecklistStarter.start template

  $scope.confirmDeleteTemplate = (template) ->
    if not $scope.canManage
        $scope.notifySave = "Permission Denied"
        return

    modalInstance = $modal.open
      templateUrl: '<%= asset_path("partials/template_delete.html") %>'
      controller: TemplateDeleteCtrl
      resolve:
        template: ->
          template

    modalInstance.result.then () ->
      $scope.deleteTemplate(template)

  $scope.deleteTemplate = (template) ->
    Templates.delete {id: template.id},
      (data) ->
        # Success
        index = $scope.templates.indexOf(template)
        $scope.templates.splice(index,1)
        $rootScope.template_count = $scope.templates.length
    , (data) ->
        # Error
        $scope.error = "Error deleting template '#{template.name}'"

  $scope.clearError = ->
    $scope.error = null

  $scope.editChecklist = (template) ->
    $location.path("/templates/edit/#{template.id}")

  $scope.$watch "search", (value) ->
    if value
      $scope.templates = _.filter($scope.allTemplates, (t) ->
          t.name.toLowerCase().indexOf(value.toLowerCase()) != -1
        )
    else
      $scope.templates = $scope.allTemplates
  , true

  TemplateCategories.query {}, (data) ->
    $scope.categories = _.map(data, (item) ->
      return item if $scope.display.categories.indexOf(item.id) is -1

      item.selected = true
      return item
    )
    $scope.getTemplates()

  , (data) ->
      $scope.error = "Error retrieving information from server"
